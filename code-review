#!/usr/bin/env zsh

set -e

base_branch=${1:-${GIT_BASE_BRANCH:-master}}
target_branch=${2:-HEAD}

merge_base=$(git merge-base $target_branch $base_branch)

shortstatout=$(git diff --shortstat --color $merge_base $target_branch)
statout=$(git diff --stat --color $merge_base $target_branch)
filesout=$(git diff --name-only $merge_base $target_branch)

unset LESS

function cleanup () {
  echo -ne "\e[?1049l"
  exit
}
trap "cleanup" 2

while true; do
  # alternate screen
  echo -ne "\e[?1049h"
  # clear screen; move to top left
  echo -ne "\e[2J\e[H"
  echo "comparing $base_branch..$target_branch"
  echo $shortstatout
  echo "Usage: l - list changed files, f - launch difftool for file, q - quit"
  read -sk opt
  case $opt in
    l)
      echo $statout | less -c -g -i -M -R -S -w -X -z-4
      ;;
    f)
      file=$(echo "$filesout" | fzf) && git difftool --no-prompt -x 'nvim -R -d' $merge_base $target_branch -- $file
      ;;
    q)
      break
      ;;
  esac
done

# revert alternate screen
cleanup
